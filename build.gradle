//----------------------------------------- BUILD SCRIPT ------------------------------------------------//

buildscript {
  repositories {
  	mavenLocal()
	mavenCentral()
	maven {
		name = "GradleRIO"
		url = "http://dev.imjac.in/maven"
	}
  }
  dependencies {
    classpath group: 'jaci.openrio.gradle', name: 'GradleRIO', version: '+', classifier: 'Toast'			//Change this line if you wish to Update GradleRIO
  }
}

//----------------------------------------- PLUGINS DEFINITION ------------------------------------------------//

plugins {
    id 'java'
    id 'java-gradle-plugin'    
    id 'idea'
    id 'eclipse'
    id 'maven'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}
apply plugin:'GradleRIO'  

//----------------------------------------- TOAST CONFIGURATION ------------------------------------------------//
// This is a copy from the Toast Setup. Is is defined even with the repository definition above in the buildscript
repositories {
    mavenCentral()
    maven {
        name = "Toast"
        url = "http://dev.imjac.in/maven"
    }
}
dependencies {

	// Robot dependencies
    compile group: 'jaci.openrio.toast', name: 'Toast', version: '+'    //Change this line to update Toast
    
    // Web dependencies
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.14.v20161028'
	compile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: '9.3.12.v20160915'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-servlet', version: '9.0.0.M3'
    compile group: 'org.json', name: 'json', version: '20160810'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-client', version: '9.0.0.M0'
    
    // Test dependencies
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

//----------------------------------------- TEAM INFO MANAGEMENT ------------------------------------------------//

gradlerio.robotClass = "org.usfirst.frc.team4571.Robot"   //The class for the main Robot Class. Used in manifest
gradlerio.team = "4571"                                   //Your FRC team number (e.g. 5333 for team 'Can't C#', or 47 for Chief Delphi)
gradlerio.rioIP = "10.45.71.20"                         //Uncomment to specify the IP address of the RIO

//----------------------------------------- BUILDING THE JAR ------------------------------------------------//

def robotManifest = {
  attributes 'Main-Class': 'edu.wpi.first.wpilibj.RobotBase'
  attributes 'Robot-Class': gradlerio.robotClass
  attributes 'Implementation-Title': 'Crate Robot'
}

jar {
  baseName = project.name
  from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  manifest robotManifest
}

task genJavadoc(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives genJavadoc
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
        resources {
            srcDirs = ['test/resources']
        }
    }
}

// Appending a project version to the jar. version is passed in with -PprodVersion=...
allprojects {
    if (project.hasProperty('prodVersion')) {
        project.version = project.prodVersion
    } else {
        project.version = 'developer-build'
    }
}

//----------------------------------------- CODE COVERAGE REPORTS ------------------------------------------------//

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}